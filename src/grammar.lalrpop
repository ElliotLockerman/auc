
use std::collections::HashMap;

use std::str::FromStr;
use ast::*;


grammar;


pub Float: f64 = <r#"-?(0|[1-9][0-9]*)(\.([0-9]+))?([eE]([-+]?[0-9]+))?"#> => f64::from_str(<>).unwrap();

pub UnitName: String = <r#"[a-zA-Z_]+"#> => <>.to_string();

pub Unit: (String, i32) = {
    <UnitName> => (<>, 1),
    <n:UnitName> "^" <f:Float> => (n, f as i32),
};



pub N: Num = {
    <Float> => Num{val: <>, units: HashMap::new()},
    <Unit+> => Num{val:1.0, units: <>.iter().cloned().collect()},
    <f:Float> <u:Unit+> => Num{val:f, units: u.iter().cloned().collect()},
}


pub Exp: Expression = {
    <e:Exp> "+" <t:T> => Expression::Sum(Box::new(e), Box::new(t)),
    <e:Exp> "-" <t:T> => Expression::Diff(Box::new(e), Box::new(t)),
    T => Expression::Term(Box::new(<>)),
}


pub T: Term = {
    <t:T> "*" <f:Fact> => Term::Prod(Box::new(t), Box::new(f)),
    <t:T> "/" <f:Fact> => Term::Quot(Box::new(t), Box::new(f)),
    Fact => Term::Factor(Box::new(<>)),
}

pub Fact: Factor = {
    N => Factor::Num(<>),
    "(" <e1:Exp> ")" "(" <e2:Exp> ")" => Factor::Prod(Box::new(e1),Box::new(e2)),
    <f1:Float> "^" <f2:Float> => Factor::Num(Num{val: f1.powf(f2), units: HashMap::new()}),
    // <n:Num> "^" "(" <e:Exp> ")" => ast::Factor::NPowE(n, Box::new(e)),
    "(" <e:Exp> ")" => Factor::Exp(Box::new(e)),
    "(" <e1:Exp> ")" "^" <n:N> => Factor::EPowN(Box::new(e1), n),
    "(" <e1:Exp> ")" "^" "(" <e2:Exp> ")" => Factor::EPowE(Box::new(e1), Box::new(e2)),
}




